apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    //변수 선언
    def vName
    def vCode
    def versionPropsFile = file('version.properties')

    //파일 읽기
    if(versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        // 키값 대입
        vName = versionProps['VERSION_NAME']
        vCode = versionProps['VERSION_CODE'].toInteger()
    } else {
        throw new FileNotFoundException("Could not read version.properties.")
    }

    defaultConfig {
        applicationId "com.gaeng0517.kotlinpractice"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode vCode
        versionName vName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation deps.kotlin_stdlib
    implementation deps.support.core_ktx
    implementation deps.support.app_compat
    implementation deps.support.constraint_layout
    implementation deps.google.android_mateiral
    implementation 'androidx.navigation:navigation-fragment:2.3.3'
    implementation 'androidx.navigation:navigation-ui:2.3.3'
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9")

    testImplementation deps.test.junit
    androidTestImplementation deps.test.atsl_junit
    androidTestImplementation deps.test.espresso_core
}